buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "nu.studer:gradle-credentials-plugin:1.0.1"
    }
}

group = "io.chucknorris"

ext {
    hamcrestVersion = "1.3"
    jsonVersion = "20160810"
    junitVersion = "4.12"
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "nu.studer.credentials"

ext."signing.keyId" = credentials.signingKeyId
ext."signing.password" = credentials.signingPassword
ext."signing.secretKeyRingFile" = credentials.signingSecretKeyRingFile

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.json:json:${jsonVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
}

jar {
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version
}

javadoc {
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = project.name
    options.addStringOption("Xdoclint:none", "-quiet")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: credentials.ossrhUsername, password: credentials.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: credentials.ossrhUsername, password: credentials.ossrhPassword)
            }

            pom.project {
                name "chucknorris.io Java client"
                packaging "jar"
                description "RestFB Messenger"
                url "https://github.com/chucknorris-io/client-java"

                scm {
                    connection "scm:git@github.com/chucknorris-io/client-java.git"
                    developerConnection "scm:git@github.com:chucknorris-io/client-java.git"
                    url "https://github.com/chucknorris-io/client-java"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "marceloverdijk"
                        name "Marcel Overdijk"
                        email "marcel@overdijk.me"
                    }
                }
            }
        }
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    description = "Generates gradlew[.bat] scripts."
    gradleVersion = "3.1"
}
